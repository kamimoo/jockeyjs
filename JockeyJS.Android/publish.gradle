apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

install {
  repositories.mavenInstaller {
    pom.project {
      name POM_NAME
      description POM_DESCRIPTION
      url WEBSITE_URL
      packaging POM_PACKAGING
      groupId project.group
      artifactId POM_ARTIFACT_ID
      version project.version
    }
  }
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier = 'sources'
}

artifacts {
  archives sourcesJar
}

android.libraryVariants.all { variant ->
  if(variant.name == 'release') {
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
      // title = ''
      // description = ''
      source = variant.javaCompile.source
      classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())
      options {
        encoding "utf-8"
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
      }
      exclude '**/BuildConfig.java'
      exclude '**/R.java'
    }
    task("javadoc${variant.name.capitalize()}Jar", type: Jar, dependsOn: "generate${variant.name.capitalize()}Javadoc") {
      classifier = 'javadoc'
      from tasks.getByName("generate${variant.name.capitalize()}Javadoc").destinationDir
    }
    artifacts {
      archives tasks.getByName("javadoc${variant.name.capitalize()}Jar")
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? bintrayUser : ''
  key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''

  pkg {
    repo = 'maven'
    name = "${GROUP}:${POM_ARTIFACT_ID}"
    licenses = ['MIT']
    websiteUrl = WEBSITE_URL
    vcsUrl = VCS_URL
    version {
      name = project.version
      released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ")
      gpg {
        sign = true
        passphrase = project.hasProperty('bintrayGpgPassword') ? bintrayGpgPassword : ''
      }
    }
  }
  publications = ['mavenPublication']
  configurations = ['archives']
}

